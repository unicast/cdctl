dnl Process this file with autoconf to produce a configure script.
AC_INIT(cdctl.c)

dnl JRS: this is where THE version for cdctl is stored.  
VERSION="0.16"
AC_DEFINE(VERSION, "$VERSION", The current version of cdctl)
AC_SUBST(VERSION)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC

dnl Make sure that asm/ioctls.h comes first to catch the case that the 
dnl kernel headers aren't available or are symlinked to a Linux kernel
dnl tree that hasn't been compiled yet.
AC_CHECK_HEADERS(fcntl.h malloc.h)
AC_CHECK_HEADERS(asm/ioctls.h,,
	[
	echo "You probably don't have your kernel headers installed right."
	echo "Look at /usr/include/asm and make sure it's pointing to"
	echo "include/asm in a valid kernel source tree.  You may have"
	echo "to run 'make symlinks' or 'make config' to create the right"
	echo "links."
	exit 1
	])

AC_CHECK_HEADERS(asm/cdrom.h unistd.h linux/cdrom.h linux/errno.h sys/ioctl.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

AC_EGREP_HEADER(
	struct dvd_layer, linux/cdrom.h, [ 
		AC_DEFINE(HAVE_DVD_IOCTLS, 1, 
	   		  Define if <linux/cdrom.h> has the DVD ioctls.
		)
	]
)	

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(strstr)

dnl Where to put the documentation (NEWS, INSTALL, etc.)
AC_MSG_CHECKING(where to put the documentation)
AC_ARG_WITH(docdir,
	[  --with-docdir=PATH      Where to put the documentation [PREFIX/doc/cdctl]],
	[cdctl_cv_docdir=$withval],
	[	AC_CACHE_VAL(cdctl_cv_docdir,
			cdctl_cv_docdir='${prefix}/doc/cdctl'
			)
	])
docdir=$cdctl_cv_docdir
AC_MSG_RESULT($docdir)
AC_SUBST(docdir)

AC_CONFIG_HEADER(config.h)

AC_OUTPUT(Makefile cdctl.spec)
